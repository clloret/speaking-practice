apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.clloret.speakingpractice"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 12
        versionName "1.9.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            def propsFile = rootProject.file('secrets.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            resValue("string", "PORT_NUMBER", "8081")
        }
    }

    flavorDimensions 'default'
    productFlavors {
        develop {
            dimension 'default'
            versionNameSuffix '-develop'
            applicationIdSuffix '.develop'
        }
        play {
            dimension 'default'
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "com.google.android.material:material:1.3.0"

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:${recyclerViewVersion}"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"

    // KTX
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.activity:activity-ktx:1.2.3'
    implementation 'androidx.fragment:fragment-ktx:1.3.5'
    implementation "androidx.preference:preference-ktx:1.1.1"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"

    // Navigation
    implementation "androidx.navigation:navigation-runtime-ktx:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Firebase
    implementation 'com.google.firebase:firebase-common-ktx:20.0.0'
    implementation 'com.google.firebase:firebase-analytics-ktx:19.0.0'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.1.0'

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // SQLite
    implementation "androidx.sqlite:sqlite-ktx:2.1.0"

    implementation "com.github.MatrixDev.Roomigrant:RoomigrantLib:$roomigrantVersion"
    kapt "com.github.MatrixDev.Roomigrant:RoomigrantCompiler:$roomigrantVersion"

    // Koin
    implementation "io.insert-koin:koin-androidx-scope:$koinVersion"
    implementation "io.insert-koin:koin-androidx-viewmodel:$koinVersion"

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.20"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.github.daniel-stoneuk:material-about-library:3.1.2'
    implementation "com.vorlonsoft:androidrate:1.2.1"
    implementation 'com.github.zagum:SpeechRecognitionView:1.2.2'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // Units tests

    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13.2'

    testImplementation 'androidx.test:core-ktx:1.3.0'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.2'

    // Helper for other arch dependencies, including JUnit test rules that can be used with LiveData
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // Robolectric environment
    testImplementation 'org.robolectric:robolectric:4.5.1'

    // Truth
    testImplementation 'androidx.test.ext:truth:1.3.0'
    testImplementation 'com.google.truth:truth:1.1.2'

    // Mockito framework
    testImplementation 'org.mockito:mockito-core:3.5.15'

    // Coroutines
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2"

    testImplementation 'com.jraska.livedata:testing-ktx:1.2.0'

    // Instrumented unit tests

    // Core library
    androidTestImplementation 'androidx.test:core-ktx:1.3.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.ext:truth:1.3.0'
    androidTestImplementation 'com.google.truth:truth:1.1.2'
}
